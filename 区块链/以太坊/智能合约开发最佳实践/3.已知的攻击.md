以下是您应该知道的已知攻击列表，并在编写智能合约时进行抵御。

## 竞争条件

调用外部合同的主要风险之一是他们可以接管控制流程，并对调用函数不期望的数据进行更改。这类错误可以有多种形式，导致DAO崩溃的两个主要错误都是这类错误。

#### 重入

这个bug的第一个版本需要注意，这个函数可以在函数第一次调用完成之前重复调用。这可能会导致函数的不同调用以破坏性方式进行交互。

```
// INSECURE
mapping (address => uint) private userBalances;

function withdrawBalance() public {
    uint amountToWithdraw = userBalances[msg.sender];
    require(msg.sender.call.value(amountToWithdraw)()); // At this point, the caller's code is executed, and can call withdrawBalance again
    userBalances[msg.sender] = 0;
}
```

由于用户的余额在函数的最后才会设置为0，所以第二次（以后）的调用仍然会成功，并且会一次又一次地撤销余额。一个非常类似的错误是DAO攻击中的一个漏洞。

在给出的示例中，避免此问题的最佳方法是使用send（）而不是call.value（）。这将阻止任何外部代码被执行。

但是，如果无法删除外部调用，则防止此次攻击的下一个最简单方法是确保在完成所有需要执行的内部工作之前不要调用外部函数：

```
mapping (address => uint) private userBalances;

function withdrawBalance() public {
    uint amountToWithdraw = userBalances[msg.sender];
    userBalances[msg.sender] = 0;
    require(msg.sender.call.value(amountToWithdraw)()); // The user's balance is already 0, so future invocations won't withdraw anything
}
```

请注意，如果您有另一个调用withdrawBalance（）的函数，它可能受到相同的攻击，因此您必须将任何调用不可信合约的函数视为不可信。请参阅下面的潜在解决方案的进一步讨论。

#### 交叉功能竞争条件

攻击者也可以使用共享相同状态的两个不同功能进行类似的攻击。

```
// INSECURE
mapping (address => uint) private userBalances;

function transfer(address to, uint amount) {
    if (userBalances[msg.sender] >= amount) {
       userBalances[to] += amount;
       userBalances[msg.sender] -= amount;
    }
}

function withdrawBalance() public {
    uint amountToWithdraw = userBalances[msg.sender];
    require(msg.sender.call.value(amountToWithdraw)()); // At this point, the caller's code is executed, and can call transfer()
    userBalances[msg.sender] = 0;
}
```

在这种情况下，当他们的代码在withdrawBalance中的外部调用上执行时，攻击者调用transfer（）。由于他们的余额尚未设置为0，即使他们已经收到了退款，他们也可以转移令牌。这个漏洞也被用于DAO攻击。

相同的解决方案也会起作用，同样需要注意。还要注意在这个例子中，这两个函数都是同一个合同的一部分。但是，如果这些合同共享状态，则可能会在多个合同中发生相同的错误。

#### 竞争条件解决方案中的陷阱

由于竞争条件可能发生在多个职能部门，甚至是多个合同中，所以任何旨在防止再入的解决方案都是不够的。

相反，我们建议先完成所有内部工作，然后再调用外部函数。这个规则，如果仔细地遵循，将允许您避免竞争条件。但是，您不仅需要尽快避免调用外部函数，还要避免调用调用外部函数的函数。例如，以下是不安全的：

```
// INSECURE
mapping (address => uint) private userBalances;
mapping (address => bool) private claimedBonus;
mapping (address => uint) private rewardsForA;

function withdraw(address recipient) public {
    uint amountToWithdraw = userBalances[recipient];
    rewardsForA[recipient] = 0;
    require(recipient.call.value(amountToWithdraw)());
}

function getFirstWithdrawalBonus(address recipient) public {
    require(!claimedBonus[recipient]); // Each recipient should only be able to claim the bonus once

    rewardsForA[recipient] += 100;
    withdraw(recipient); // At this point, the caller will be able to execute getFirstWithdrawalBonus again.
    claimedBonus[recipient] = true;
}
```

尽管getFirstWithdrawalBonus（）不直接调用外部合约，但在withdraw（）中的调用足以使其容易受到竞争条件的影响。因此，您需要将withdraw（）视为不可信。

```
mapping (address => uint) private userBalances;
mapping (address => bool) private claimedBonus;
mapping (address => uint) private rewardsForA;

function untrustedWithdraw(address recipient) public {
    uint amountToWithdraw = userBalances[recipient];
    rewardsForA[recipient] = 0;
    require(recipient.call.value(amountToWithdraw)());
}

function untrustedGetFirstWithdrawalBonus(address recipient) public {
    require(!claimedBonus[recipient]); // Each recipient should only be able to claim the bonus once

    claimedBonus[recipient] = true;
    rewardsForA[recipient] += 100;
    untrustedWithdraw(recipient); // claimedBonus has been set to true, so reentry is impossible
}
```

除了修复不可能再进入外，不可信任的功能已被标记。这种模式在每个级别都会重复：因为untrustedGetFirstWithdrawalBonus（）调用untrustedWithdraw（）（调用外部协定），所以还必须将untrustedGetFirstWithdrawalBonus（）视为不安全。

另一个解决方案通常是一个互斥体。这允许您“锁定”某些状态，以便只能由锁的所有者更改。一个简单的例子可能如下所示：

```
// Note: This is a rudimentary example, and mutexes are particularly useful where there is substantial logic and/or shared state
mapping (address => uint) private balances;
bool private lockBalances;

function deposit() payable public returns (bool) {
    require(!lockBalances);
    lockBalances = true;
    balances[msg.sender] += msg.value;
    lockBalances = false;
    return true;
}

function withdraw(uint amount) payable public returns (bool) {
    require(!lockBalances && amount > 0 && balances[msg.sender] >= amount);
    lockBalances = true;

    if (msg.sender.call(amount)()) { // Normally insecure, but the mutex saves it
      balances[msg.sender] -= amount;
    }

    lockBalances = false;
    return true;
}
```

如果用户在第一次通话结束之前尝试再次调用withdraw（），则该锁定将阻止其发挥作用。这可能是一种有效的模式，但当您需要合作的多个合约时，它会变得棘手。以下是不安全的：

```
// INSECURE
contract StateHolder {
    uint private n;
    address private lockHolder;

    function getLock() {
        require(lockHolder == 0);
        lockHolder = msg.sender;
    }

    function releaseLock() {
        require(msg.sender == lockHolder);
        lockHolder = 0;
    }

    function set(uint newState) {
        require(msg.sender == lockHolder);
        n = newState;
    }
}
```

攻击者可以调用getLock（），然后再不调用releaseLock（）。如果他们这样做，那么合同将永远被锁定，并且不能做出进一步的改变。如果您使用互斥体来防止竞争条件，您需要小心确保没有办法锁定声明并且永远不会释放。 （使用互斥锁编程时还有其他潜在的危险，例如死锁和活锁。如果您决定采用这种方式，您应该查阅大量已经写入互斥锁的文献。）

*有些人可能反对使用术语竞争条件，因为以太坊目前没有真正的并行性。然而，逻辑上不同的进程争夺资源的基本特征仍然存在，同样的陷阱和潜在的解决方案也适用。

## 交易顺序依赖（TOD）/前台运行

以上是涉及攻击者在单个事务中执行恶意代码的竞争条件示例。以下是区块链固有的一种不同类型的竞争条件：事务本身（在区块内）的顺序很容易受到操控。

由于交易在短时间内处于流通中，因此可以知道在包含在块中之前会发生什么行为。对于像分散市场这样的事情来说，这可能会很麻烦，在这种市场上可以看到买入某些代币的交易，并且在另一个交易被包括之前执行市场订单。防止这种情况很困难，因为它会涉及具体合同本身。例如，在市场中，实施批量拍卖会更好（这也可以防止高频交易问题）。另一种使用预先提交方案的方法（“我将在稍后提交细节”）。

## 时间戳依赖

请注意，矿工的时间戳可以由矿工操纵，并应考虑时间戳的所有直接和间接使用。

有关与时间戳相关的设计注意事项，请参阅[建议](https://consensys.github.io/smart-contract-best-practices/recommendations/#timestamp-dependence)部分。

## 整数溢出和下溢

考虑一个简单的TOKEN传输：

```
mapping (address => uint256) public balanceOf;

// INSECURE
function transfer(address _to, uint256 _value) {
    /* Check if sender has balance */
    require(balanceOf[msg.sender] >= _value);
    /* Add and subtract new balances */
    balanceOf[msg.sender] -= _value;
    balanceOf[_to] += _value;
}

// SECURE
function transfer(address _to, uint256 _value) {
    /* Check if sender has balance and for overflows */
    require(balanceOf[msg.sender] >= _value && balanceOf[_to] + _value >= balanceOf[_to]);

    /* Add and subtract new balances */
    balanceOf[msg.sender] -= _value;
    balanceOf[_to] += _value;
}
```

如果余额达到最大uint值（2 ^ 256），它将回到零。这检查这种情况。根据实施情况，这可能相关也可能不相关。考虑一下uint值是否有机会接近如此多的数字。考虑uint变量如何改变状态，以及谁有权做出这样的改变。如果任何用户可以调用更新uint值的函数，则它更容易受到攻击。如果只有管理员有权更改变量的状态，则可能是安全的。如果用户每次只能增加1个，那么您可能也很安全，因为没有可行的方法来达到此限制。

下溢也是如此。如果uint小于零，它将导致下溢并被设置为其最大值。

小心使用像uint8，uint16，uint24等较小的数据类型：它们甚至可以更容易地达到最大值。

请注意，大约有[20起溢出和下溢事件](https://github.com/ethereum/solidity/issues/796#issuecomment-253578925)。

## 深度下溢：存储操作

[道格霍伊特提交](https://github.com/Arachnid/uscc/tree/master/submissions-2017/doughoyte)给2017年不利的健康比赛提交了[荣誉提名](http://u.solidity.cc/)。这篇文章很有意思，因为它引起了人们对C类下溢如何影响Solidity存储的担忧。这是一个简化版本：

```
contract UnderflowManipulation {
    address public owner;
    uint256 public manipulateMe = 10;
    function UnderflowManipulation() {
        owner = msg.sender;
    }

    uint[] public bonusCodes;

    function pushBonusCode(uint code) {
        bonusCodes.push(code);
    }

    function popBonusCode()  {
        require(bonusCodes.length >=0);  // this is a tautology
        bonusCodes.length--; // an underflow can be caused here
    }

    function modifyBonusCode(uint index, uint update)  { 
        require(index < bonusCodes.length);
        bonusCodes[index] = update; // write to any index less than bonusCodes.length
    }

}
```

一般来说，不通过keccak256就不会影响变量操作的位置，这是不可行的。但是，由于动态数组是按顺序存储的，如果一个恶意的演员想要改变操作方法，他们需要做的就是：

- 调用popBonusCode进行下溢（注意：Solidity缺少内置的pop方法）
- 计算manipulateMe的存储位置
- 使用modifyBonusCode修改和更新manipMe的值

实际上，这个数组会立即被指出为可疑，但被埋在更复杂的智能合约架构之下，它可以任意地允许对常量变量进行恶意更改。

在考虑使用动态数组时，容器数据结构是一种很好的做法。 Solidity CRUD[第1部分](https://medium.com/@robhitchens/solidity-crud-part-1-824ffa69509a)和[第2部分](https://medium.com/@robhitchens/solidity-crud-part-2-ed8d8b4f74ec)文章是很好的资源。

## 带有（意外）的DoS恢复

考虑一个简单的拍卖合同：

```
// INSECURE
contract Auction {
    address currentLeader;
    uint highestBid;

    function bid() payable {
        require(msg.value > highestBid);

        require(currentLeader.send(highestBid)); // Refund the old leader, if it fails then revert

        currentLeader = msg.sender;
        highestBid = msg.value;
    }
}
```

当它试图退还旧领导者时，如果退款失败，它将恢复。这意味着恶意投标人可以成为领导者，同时确保对他们地址的退款总是失败。这样，他们可以阻止其他人调用bid（）函数，并永远保持领导者的地位。如前所述，建议改为建立支付系统。

另一个例子是何时合同可以迭代数组来支付用户（例如众筹合同中的支持者）。通常要确保每次付款都成功。如果不是，则应该回复。问题是，如果一次呼叫失败，您将恢复整个付款系统，这意味着循环永远不会完成。没有人因为一个地址强制错误而获得报酬。

```
address[] private refundAddresses;
mapping (address => uint) public refunds;

// bad
function refundAll() public {
    for(uint x; x < refundAddresses.length; x++) { // arbitrary length iteration based on how many addresses participated
        require(refundAddresses[x].send(refunds[refundAddresses[x]])) // doubly bad, now a single failure on send will hold up all funds
    }
}
```

同样，推荐的解决方案是[赞成推迟付款](https://consensys.github.io/smart-contract-best-practices/recommendations#favor-pull-over-push-for-external-calls)。

## 阻止gas limit的DoS

您可能已经注意到前一个例子中的另一个问题：通过一次性支付给所有人，您有可能会遇到阻止瓦斯限制的风险。以太坊模块可以处理一定的最大计算量。如果你试图去重述一下，你的交易将会失败。

即使没有故意的攻击，这也会导致问题。但是，如果攻击者能够操纵所需的气体量，这一点尤其糟糕。在前面的例子中，攻击者可以添加一堆地址，每个地址都需要很少的退款。因此，退还每个攻击者地址的天然气成本最终可能超过燃气限额，从而阻止退款交易的发生。

这是[赞成推迟付款](https://consensys.github.io/smart-contract-best-practices/recommendations#favor-pull-over-push-for-external-calls)的另一个原因。

如果你绝对必须遍历未知大小的数组，那么你应该计划它可能需要多个块，因此需要多个事务。您将需要跟踪您已经走了多远，并且能够从这一点恢复，如下例所示：

```
struct Payee {
    address addr;
    uint256 value;
}

Payee[] payees;
uint256 nextPayeeIndex;

function payOut() {
    uint256 i = nextPayeeIndex;
    while (i < payees.length && msg.gas > 200000) {
      payees[i].addr.send(payees[i].value);
      i++;
    }
    nextPayeeIndex = i;
}
```

您需要确保在等待payOut（）函数的下一次迭代时处理其他事务时不会发生任何错误。所以只有在绝对必要时才使用这种模式。

## 强行将ETHER送达合同

可以将Ether强制发送到合同而不触发其后退功能。将重要逻辑放在后备功能或根据合同的平衡进行计算时，这是一个重要的考虑因素。以下面的例子：

```
contract Vulnerable {
    function () payable {
        revert();
    }

    function somethingBad() {
        require(this.balance > 0);
        // Do something bad
    }
}
```

合同逻辑似乎不允许对合同付款，因此不允许发生“不好的事情”。但是，有几种方法强制将乙醚送达合同，因此使其余额大于零。

自毁合约方法允许用户指定受益人发送任何多余的以太。 selfdestruct不会[触发合约的后备功能](https://solidity.readthedocs.io/en/develop/security-considerations.html#sending-and-receiving-ether)。

在部署合同之前，还可以[预先计算](https://github.com/Arachnid/uscc/tree/master/submissions-2017/ricmoo)合同地址并将Ether发送到该地址。

合同开发者应该意识到以太可以被强制送到合同，并且应该相应地设计契约逻辑。一般情况下，假设无法限制您的合同资金来源。

## 弃用/历史攻击

这些攻击由于协议的改变或固体的改进而不再可能发生。他们在这里被记录为后代和意识。

#### 深度攻击（不推荐）

至于[EIP 150](https://github.com/ethereum/EIPs/issues/150) hardfork，深度攻击不再相关*（在达到1024次深度调用限制之前，所有的gas将会被全部消费）。

[本篇原文链接](https://consensys.github.io/smart-contract-best-practices/known_attacks/)

