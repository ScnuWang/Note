本节展示了编写智能合约时应遵循的一些Solidarity模式。

## 协议相关建议

以下建议适用于任何基于以太坊智能合约的开发。



### 外部调用

#### 注意外部调用

调用不信任的合约时，可能会产生以下意想不到的风险或者错误。外部调用可能会执行该合约或者该合约依赖的其他合约中的病毒代码。因此，每个外部调用都应被视为潜在的安全风险。如果不可能或不希望删除外部调用，请使用本节其余部分的建议来减少危险。



#### 标记不可信的合同

在与外部合同进行交互时，以一种明确指出与其交互的方式来命名变量，方法和合同接口可能是不安全的。这适用于调用外部合同的自己的函数。

```solidity
// bad
Bank.withdraw(100); // Unclear whether trusted or untrusted

function makeWithdrawal(uint amount) { // Isn't clear that this function is potentially unsafe
    Bank.withdraw(amount);
}

// good
UntrustedBank.withdraw(100); // untrusted external call
TrustedBank.withdraw(100); // external but trusted bank contract maintained by XYZ Corp

function makeUntrustedWithdrawal(uint amount) {
    UntrustedBank.withdraw(amount);
}
```

#### 外部调用后避免状态更改

无论使用原始调用（形式为someAddress.call（））还是使用合同调用（形式为ExternalContract.someMethod（）），都假定可能会执行恶意代码。即使ExternalContract不是恶意的，恶意代码也可以由其调用的任何合同执行。

恶意代码可能会劫持控制流程，导致竞争状况。 （请参阅[竞争条件](https://consensys.github.io/smart-contract-best-practices/known_attacks#race-conditions)以更全面地讨论这个问题）。

如果您正在调用一个不受信任的外部合同，避免外部调用后状态更改。这种模式有时也被称为[检查效果 - 交互模式](http://solidity.readthedocs.io/en/develop/security-considerations.html?highlight=check%20effects#use-the-checks-effects-interactions-pattern)。

#### 请注意send（），transfer（）和call.value（）之间的权衡。

发送ether时应注意使用someAddress.send（），someAddress.transfer（）和someAddress.call.value（）（）之间的相对权衡。

- someAddress.send（）和someAddress.transfer（）被认为对于重入是安全的。尽管这些方法仍然会触发代码执行，但被叫合同只能获得2,300个gas的补助，目前这些补助仅足以记录事件。
- x.transfer（y）等同于require（x.send（y））;如果发送失败，它将自动恢复。
- someAddress.call.value（y）会发送提供的以太和触发代码执行。 执行的代码被赋予所有可用的执行气体，使得这种类型的值传输对于重入不安全。

使用send（）或transfer（）可以防止重入，但是这样做的代价是与任何其后备功能需要超过2,300个gas的合同不相容。也可以使用someAddress.call.value（ethAmount）.gas（gasAmount）来转发一定量的gas。

试图平衡这种权衡的一种模式是使用send（）或transfer（）作为push组件，call.value（）作为pull组件实现push和pull机制。

值得指出的是，对于价值转移而言，专用send（）或transfer（）本身并不能使合约安全以避免重新进入，而只能使这些特定的价值转移安全地避免重新进入。

#### 处理外部调用中的错误

Solidity提供在原始地址上工作的低级调用方法：address.call（），address.callcode（），address.delegatecall（）和address.send（）。这些低级方法从不抛出异常，但是如果调用遇到异常则返回false。另一方面，合同调用（例如，ExternalContract.doSomething（））会自动传播一个throw（例如，如果抛出doSomething（），ExternalContract.doSomething（）也会抛出）。

如果您选择使用低级别的调用方法，请确保通过检查返回值来处理调用失败的可能性。

```
// bad
someAddress.send(55);
someAddress.call.value(55)(); // this is doubly dangerous, as it will forward all remaining gas and doesn't check for result
someAddress.call.value(100)(bytes4(sha3("deposit()"))); // if deposit throws an exception, the raw call() will only return false and transaction will NOT be reverted

// good
if(!someAddress.send(55)) {
    // Some failure code
}

ExternalContract(someAddress).deposit.value(100);
```

外部调用可能意外或故意失败。为了尽量减少由此类故障引起的损害，通常最好将每个外部调用隔离到可由调用接收方发起的自己的交易中。这与付款特别相关，最好让用户提取资金，而不是自动向他们提供资金。 （这也减少了gas limit 问题的机会。）避免在单个事务中组合多个send（）调用。

```
// bad
contract auction {
    address highestBidder;
    uint highestBid;

    function bid() payable {
        require(msg.value >= highestBid);

        if (highestBidder != 0) {
            highestBidder.transfer(highestBid); // if this call consistently fails, no one else can bid
        }

       highestBidder = msg.sender;
       highestBid = msg.value;
    }
}

// good
contract auction {
    address highestBidder;
    uint highestBid;
    mapping(address => uint) refunds;

    function bid() payable external {
        require(msg.value >= highestBid);

        if (highestBidder != 0) {
            refunds[highestBidder] += highestBid; // record the refund that this user can claim
        }

        highestBidder = msg.sender;
        highestBid = msg.value;
    }

    function withdrawRefund() external {
        uint refund = refunds[msg.sender];
        refunds[msg.sender] = 0;
        msg.sender.transfer(refund);
    }
}
```

#### 不要假设合同是以零余额创建的

攻击者可以在创建之前将wei发送到合同地址。合同不应该假设其初始状态包含零余额。有关更多详情，请参阅[issue61](https://github.com/ConsenSys/smart-contract-best-practices/issues/61)。

#### 请记住，链上数据是公开的

许多应用程序要求提交的数据在某个时间点保持专用，以便工作。游戏（如链上摇滚剪刀）和拍卖机制（例如密封拍卖的第二价格拍卖）是两个主要类别的例子。如果您正在构建隐私问题的应用程序，请注意避免要求用户过早发布信息。

例如：

- 在摇滚纸剪刀中，要求双方首先提交他们打算移动的哈希，然后要求双方都提交移动;如果提交的移动与哈希不匹配，则抛出。
- 在拍卖中，要求玩家在初始阶段提交他们的投标价值（以及高于其投标价值的存款），然后在第二阶段提交他们的投标价值。
- 在开发依赖随机数发生器的应用程序时，顺序应该总是（1）玩家提交移动，（2）生成随机数，（3）玩家付费。产生随机数的方法本身就是一个积极研究的领域;目前同类最佳的解决方案包括比特币块头（通过http://btcrelay.org进行验证），散列提交揭示方案（即一方产生一个数字，将其散列值发布为“提交”该值，以及然后显示值）和[RANDAO](http://github.com/randao/randao)。
- 如果您正在实施频繁的批量拍卖，则还需要散列提交方案。

#### 在双方或N方合同中，请注意有些参与者可能“脱机”而不返回

不要依赖于特定方执行特定行动的退款或索赔流程，也不能以其他方式获得资金。例如，在摇滚纸剪刀游戏中，一个常见的错误是在两个玩家提交动作之前都不支付奖金;然而，恶意的玩家可以通过从不提交他们的举动来“哀悼”对方 - 事实上，如果玩家看到其他玩家的暴露举动并确定他们输了，他们就没有理由提交他们自己的举动了。这个问题也可能出现在国家渠道结算的背景下。当这种情况是一个问题时，（1）提供一种规避未参与的参与者的方式，可能是通过时间限制，并且（2）考虑增加额外的经济诱因，让参与者在所有情况下提交信息应该这样做。



## Solidity 相关建议

以下建议是特定于Solidity的，但对于开发其他语言的智能合约也可能具有指导意义。

#### 正确使用assert（）和require（）

在Solidity 0.4.10中引入了assert（）和require（）。 require（条件）用于输入验证，应该在任何用户输入上完成，并且如果条件为假，则返回。如果条件为假，assert（条件）也会恢复，但只应用于不变量：内部错误或检查您的合同是否已达到无效状态。遵循这种范式允许形式化分析工具来验证无法访问无效操作码：这意味着代码中的任何不变量都不会被违反，并且代码被正式验证。

#### 小心整数除法

所有整数除法舍入到最接近的整数。如果您需要更高的精度，请考虑使用乘数，或存储分子和分母。未来，Solidity将有一个定点类型，这将使这更容易。）

```
// bad
uint x = 5 / 2; // Result is 2, all integer divison rounds DOWN to the nearest integer
```

使用乘法器可以防止四舍五入，在将来使用x时需要考虑这个乘数：

```
// good
uint multiplier = 10;
uint x = (5 * multiplier) / 2;
```

存储分子和分母意味着您可以计算分子/分母off-chain的结果：

```
// good
uint numerator = 5;
uint denominator = 2;
```

#### 请记住，以太可以被强制发送到一个帐户

谨防编码严格检查合同平衡的不变量。

攻击者可以将wei强行发送到任何帐户，这是不能被阻止的（甚至没有使用revert（）的后备功能）。

攻击者可以通过创建合同，用1 wei资助它并调用selfdestruct（victimAddress）来实现此目的。在victimAddress中没有代码被调用，所以不能被阻止。

#### 了解抽象合同和接口之间的权衡

接口和抽象合同都为智能合同提供了一种可自定义和可重用的方法。在Solidity 0.4.11中引入的接口与抽象合约类似，但不能实现任何功能。接口也有一些限制，例如无法访问存储或从其他接口继承，这些接口通常会使抽象合约更实用。尽管接口在实施之前对设计合同非常有用。此外，请记住，如果合约从抽象合约继承，它必须通过覆盖来实现所有未实现的函数，否则它也将是抽象的。

#### 保持后备功能简单

当合同发送一个没有参数的消息时（或没有函数匹配时），调用回退函数，并且只有在从.send（）或.transfer（）调用时才能访问2,300个气体。如果您希望能够从.send（）或.transfer（）接收Ether，您可以在回退函数中执行的操作最多的是记录一个事件。如果需要计算或更多的gas，请使用适当的功能。

```
// bad
function() payable { balances[msg.sender] += msg.value; }

// good
function deposit() payable external { balances[msg.sender] += msg.value; }

function() payable { LogDepositReceived(msg.sender); }
```

#### 显式标记函数和状态变量的可见性

显式标注函数和状态变量的可见性。函数可以被指定为外部的，公共的，内部的或私有的。请理解它们之间的差异，例如，外部可能足以代替公开。对于状态变量，外部是不可能的。明确标记可见性将更容易捕捉有关谁可以调用函数或访问变量的错误假设。

```
// bad
uint x; // the default is internal for state variables, but it should be made explicit
function buy() { // the default is public
    // public code
}

// good
uint private y;
function buy() external {
    // only callable externally
}

function utility() public {
    // callable externally, as well as internally: changing this code requires thinking about both cases.
}

function internalAction() internal {
    // internal code
}
```

#### 将编译指示锁定到特定的编译器版本

应该使用相同的编译器版本部署合同，并标记它们已经过最多测试。锁定编译指示可帮助确保合同不会意外部署，例如使用最新的编译器，这些编译器可能具有未发现错误的较高风险。合同也可能由其他人部署，编译指示原始作者打算使用的编译器版本。

```
// bad
pragma solidity ^0.4.4;


// good
pragma solidity 0.4.4;
```

##### 例外

当合同打算供其他开发人员使用时，可以允许Pragma语句浮动，例如库或EthPM包中的合同。否则，开发人员需要手动更新编译指示以便在本地进行编译。



#### 区分功能和事件

对于事件，建议以Log为前缀，以防止函数和事件之间混淆的风险。对于函数，除了构造函数外，总是以小写字母开头。

```
// bad
event Transfer() {}
function transfer() {}

// good
event LogTransfer() {}
function transfer() external {}
```

#### 偏好较新的Solidity结构

更喜欢构造/别名，比如selfdestruct（超过suicide）和keccak256（超过sha3）。类似require（msg.sender.send（1 ether））的模式也可以简化为使用transfer（），如在msg.sender.transfer（1 ether）中一样。

#### 请注意，“内置”可能会被遮蔽

目前可以在Solidity中隐藏内置的全局变量。这允许合同覆盖诸如msg和revert（）之类的内置函数。虽然这是有意的，但它可能误导合同的用户关于合同的真实行为。

```
contract PretendingToRevert {
    function revert() internal constant {}
}

contract ExampleContract is PretendingToRevert {
    function somethingBad() public {
        revert();
    }
}
```

合同用户（和审计人员）应该知道他们打算使用的任何应用程序的完整智能合同源代码。

#### 避免使用tx.origin

切勿使用tx.origin进行授权，另一个合同可以有一个方法来调用您的合同（例如，用户有一些资金），并且您的合同将授权该交易，因为您的地址位于tx.origin中。

```
pragma solidity 0.4.18;

contract MyContract {

    address owner;

    function MyContract() public {
        owner = msg.sender;
    }

    function sendTo(address receiver, uint amount) public {
        require(tx.origin == owner);
        receiver.transfer(amount);
    }

}

contract AttackingContract {

    MyContract myContract;
    address attacker;

    function AttackingContract(address myContractAddress) public {
        myContract = MyContract(myContractAddress);
        attacker = msg.sender;
    }

    function() public {
        myContract.sendTo(attacker, msg.sender.balance);
    }

}
```

您应该使用msg.sender进行授权（如果另一个合同调用您的合同，则msg.sender将成为合同的地址，而不是调用合同的用户的地址）。

你可以在这里读更多关于它的内容: [Solidity docs](https://solidity.readthedocs.io/en/develop/security-considerations.html#tx-origin)

除了授权问题外，将来有可能从以太坊协议中删除tx.origin，因此使用tx.origin的代码将与未来版本不兼容[Vitalik: 'Do NOT assume that tx.origin will continue to be usable or meaningful.'](https://ethereum.stackexchange.com/questions/196/how-do-i-make-my-dapp-serenity-proof/200#200)

另外值得一提的是，通过使用tx.origin，您限制了合同之间的互操作性，因为使用tx.origin的合同不能被另一个合同使用，因为合同不能是tx.origin。

## 时间戳依赖

在合同中使用时间戳执行关键功能时有三个主要考虑因素，特别是当涉及资金转移时。

#### 可玩性

请注意矿工的时间戳可以被矿工操纵。考虑这个[合同](https://etherscan.io/address/0xcac337492149bdb66b088bf5914bedfbf78ccc18#code)：

```
uint256 constant private salt =  block.timestamp;

function random(uint Max) constant private returns (uint256 result){
    //get the best seed for randomness
    uint256 x = salt * 100/Max;
    uint256 y = salt * block.number/(salt % 5) ;
    uint256 seed = block.number/3 + (salt % 300) + Last_Payout + y; 
    uint256 h = uint256(block.blockhash(seed)); 

    return uint256((h / x)) % Max + 1; //random number between 1 and Max
}
```

当合约使用时间戳来产生随机数时，矿工实际上可以在被验证块的30秒内发布时间戳，从而有效地允许矿工预先计算一个更有利于他们抽奖机会的选项。时间戳不是随机的，不应该在这种情况下使用。

#### 30秒规则

评估时间戳使用的一般经验法则是：如果合同功能可以容忍30秒的时间漂移，则使用block.timestamp是安全的。

如果您的时间相关事件的比例可以变化30秒并保持完整性，则使用时间戳是安全的。这包括拍卖结束，注册期限等事情。

#### 注意使用block.number作为时间戳

当合约创建auction_complete修饰符表示拍卖结束：

```
modifier auction_complete {
    require(auctionEndBlock <= block.number     ||
          currentAuctionState == AuctionState.success || 
          currentAuctionState == AuctionState.cancel)
        _;}
```

block.number和 [average block time](https://etherscan.io/chart/blocktime)也可以用来估计时间，但这并不是未来的证明，因为block time可能会改变（例如[fork reorganisations](https://blog.ethereum.org/2015/08/08/chain-reorganisation-depth-expectations/)和[difficulty bomb](https://github.com/ethereum/EIPs/issues/649)）。在一天的销售中，12分钟规则允许人们构建更可靠的时间估计。

#### 谨慎多重继承

当在Solidity中使用多重继承时，了解编译器如何组成继承图很重要。

```
contract Final {
    uint public a;
    function Final(uint f) public {
        a = f;
    }
}

contract B is Final {
    int public fee;

    function B(uint f) Final(f) public {
    }
    function setFee() public {
        fee = 3;
    }
}

contract C is Final {
    int public fee;

    function C(uint f) Final(f) public {
    }
    function setFee() public {
        fee = 5;
    }
}

contract A is B, C {
  function A() public B(3) C(5) {
      setFee();
  }
}
```

当部署A时，编译器将从左到右线性化继承，如下所示：

**C -> B -> A**

线性化的结果将产生5的费用值，因为C是派生最多的合同。这看起来很明显，但想象一下C能够影响关键功能，重新排列布尔条款并导致开发人员编写可利用合约的场景。静态分析目前不会引起遮盖功能的问题，因此必须手动进行检查。

有关安全性和继承的更多信息，请查看[本文](https://pdaian.com/blog/solidity-anti-patterns-fun-with-inheritance-dag-abuse/)

为了做出贡献，Solidity的Github有一个包含所有遗产相关问题的[项目](https://github.com/ethereum/solidity/projects/9#card-8027020)。

#### 已弃用/历史建议

这些建议由于协议的改变或Solidity的改进而不再相关。他们在这里被记录为后代和意识。

比如编译器版本低于0.4的。

[本篇原文链接](https://consensys.github.io/smart-contract-best-practices/recommendations/)

