正如我们在[整体原则](https://consensys.github.io/smart-contract-best-practices/general_philosophy/)部分所讨论的那样，仅仅保护自己免受已知的攻击是不够的。由于区块链上的失败成本可能非常高，因此您还必须调整自己编写软件的方式，以解决该风险。

我们倡导的方法是“为失败做准备”。预先知道您的代码是否安全是不可能的。但是，您可以通过一种方式构建合同，使其能够优雅地失败并且损失最小。本节介绍将帮助您准备失败的各种技术。

注意：将新组件添加到系统时总会有风险。设计错误的防故障本身可能会成为一个漏洞 - 许多设计良好的故障保险箱之间的交互也可能会变成一个漏洞。仔细考虑您在合同中使用的每种技术，并仔细考虑它们如何协同工作以创建一个强大的系统。

## 升级破碎的合同

如果发现错误，代码就需要改变或者改进。

设计一个有效的智能合约升级系统是一个积极研究的领域，我们将无法涵盖本文档中的所有复杂问题。但是，有两种最常用的基本方法。两者中较简单的是拥有一份注册合同，该合同包含合同最新版本的地址。对于合同用户而言，更为无缝的方法是签订一份合同，将调用和数据转发到合同的最新版本。

无论采用哪种技术，模块化和组件之间的良好分离非常重要，以便代码更改不会中断功能，数据孤立或需要大量成本进行移植。尤其是，将复杂逻辑与数据存储区分开通常是有益的，这样您就不必重新创建所有数据以便更改功能。

为各方决定升级代码提供一种安全的方式也至关重要。根据您的合同，代码更改可能需要由单个受信任方，一组成员或全部利益相关方的投票批准。如果这个过程需要一些时间，您需要考虑是否有其他方法可以在发生攻击时迅速作出反应，如[紧急停止或断路器](https://consensys.github.io/smart-contract-best-practices/software_engineering/#circuit-breakers-pause-contract-functionality)。

示例1：使用注册协议来存储合同的最新版本



```
contract SomeRegister {
    address backendContract;
    address[] previousBackends;
    address owner;

    function SomeRegister() {
        owner = msg.sender;
    }

    modifier onlyOwner() {
        require(msg.sender == owner)
        _;
    }

    function changeBackend(address newBackend) public
    onlyOwner()
    returns (bool)
    {
        if(newBackend != backendContract) {
            previousBackends.push(backendContract);
            backendContract = newBackend;
            return true;
        }

        return false;
    }
}
```

这种方法有两个主要的缺点：

1. 用户必须始终查看当前地址，任何未能这样做的人都会使用旧版本的合同承担风险
2. 在更换合同时，您需要仔细考虑如何处理合同数据

另一种方法是让合同将调用和数据转发给合同的最新版本：

示例2：[使用DELEGATECALL](http://ethereum.stackexchange.com/questions/2404/upgradeable-contracts)转发数据和调用

```
contract Relay {
    address public currentVersion;
    address public owner;

    modifier onlyOwner() {
        require(msg.sender == owner);
        _;
    }

    function Relay(address initAddr) {
        currentVersion = initAddr;
        owner = msg.sender; // this owner may be another contract with multisig, not a single contract owner
    }

    function changeContract(address newVersion) public
    onlyOwner()
    {
        currentVersion = newVersion;
    }

    function() {
        require(currentVersion.delegatecall(msg.data));
    }
}
```

这种方法避免了以前的问题，但有其自身的问题。您必须非常小心如何在本合同中存储数据。如果您的新合同具有与第一个不同的存储布局，那么您的数据可能最终损坏。另外，这个简单版本的模式不能从函数返回值，只能转发它们，这限制了它的适用性。 （[更复杂的实现](https://github.com/ownage-ltd/ether-router)尝试使用内联汇编代码和返回大小注册表来解决此问题。）

不管你的方法如何，重要的是要有一些方法来升级你的合同，否则当发现不可避免的错误时它们将变得无法使用。

## 断路器（暂停合同功能）

如果某些条件得到满足，断路器将停止执行，并且在发现新错误时可能很有用。例如，如果发现错误，大多数操作可能会被暂停在合同中，并且现在唯一有效的操作是退出。您可以让某些可信任方能够触发断路器，也可以让程序规则在满足特定条件时自动触发某个断路器。

例：

```
bool private stopped = false;
address private owner;

modifier isAdmin() {
    require(msg.sender == owner);
    _;
}

function toggleContractActive() isAdmin public {
    // You can add an additional modifier that restricts stopping a contract to be based on another action, such as a vote of users
    stopped = !stopped;
}

modifier stopInEmergency { if (!stopped) _; }
modifier onlyInEmergency { if (stopped) _; }

function deposit() stopInEmergency public {
    // some code
}

function withdraw() onlyInEmergency public {
    // some code
}
```

## 减速（延迟合同行为）

加速行动减缓了行动，因此如果发生恶意行为，还有时间恢复。例如，DAO需要27天才能完成拆分DAO的请求和能力。这确保了资金保持在合同范围内，增加了恢复的可能性。在DAO的情况下，在减速时间内没有采取有效的措施，但结合我们的其他技术，它们可能非常有效。

例：

```
struct RequestedWithdrawal {
    uint amount;
    uint time;
}

mapping (address => uint) private balances;
mapping (address => RequestedWithdrawal) private requestedWithdrawals;
uint constant withdrawalWaitPeriod = 28 days; // 4 weeks

function requestWithdrawal() public {
    if (balances[msg.sender] > 0) {
        uint amountToWithdraw = balances[msg.sender];
        balances[msg.sender] = 0; // for simplicity, we withdraw everything;
        // presumably, the deposit function prevents new deposits when withdrawals are in progress

        requestedWithdrawals[msg.sender] = RequestedWithdrawal({
            amount: amountToWithdraw,
            time: now
        });
    }
}

function withdraw() public {
    if(requestedWithdrawals[msg.sender].amount > 0 && now > requestedWithdrawals[msg.sender].time + withdrawalWaitPeriod) {
        uint amountToWithdraw = requestedWithdrawals[msg.sender].amount;
        requestedWithdrawals[msg.sender].amount = 0;

        require(msg.sender.send(amountToWithdraw));
    }
}
```

## 费率限制

限速暂停或需要批准实质性更改。例如，只允许存款人在特定时间段内（例如，在1天内最多100个以上的存款）提取一定数量或百分比的总存款 - 在该时间段内的额外提款可能失败或需要某种特殊批准。或者，费率限制可以在合同级别，合约在一段时间内只发放一定数量的令牌。

[例](https://gist.github.com/PeterBorah/110c331dca7d23236f80e69c83a9d58c#file-circuitbreaker-sol)

## 合同分包

在大量资金面临风险之前，合约应该有相当长的测试周期。

至少，您应该：

- 拥有100％测试覆盖率（或接近它）的完整测试套件
- 部署在您自己的测试网上
- 在公共测试网上部署大量测试和错误奖励
- 详尽的测试应该允许各种玩家在数量上与合同进行交互
- 在beta版中部署在主网上，限制风险的数量

#### 自动拒绝

在测试过程中，您可以在一段时间后通过阻止任何操作强制自动弃用。例如，一个阿尔法合同可能会工作几个星期，然后自动关闭所有的行动，除了最后的撤回。

```
modifier isActive() {
    require(block.number <= SOME_BLOCK_NUMBER);
    _;
}

function deposit() public isActive {
    // some code
}

function withdraw() public {
    // some code
}
```

#### 限制每位用户/合同的数量

在早期阶段，您可以限制任何用户（或整个合同）的ETHER数量 - 降低风险。

## Bug赏金计划

运行赏金程序的一些提示：

- 决定使用哪种货币奖励（BTC和/或ETH）
- 决定赏金奖励的预计总预算
- 从预算中确定三层奖励：
- 你愿意发放的最小奖励
- 可以获取的最高奖励
- 如果存在非常严重的漏洞，将额外给予范围
- 确定赏金评委是谁（3人可能是典型的理想人选）
- 首席开发者可能应该是赏金评委之一
- 当收到错误报告时，首席开发人员应该根据评委的建议评估错误的严重程度
- 在这个阶段的工作应该是在一个私人的回购，并在Github上提出的问题
- 如果这是一个应该修复的bug，那么在私人回购中，开发人员应该编写一个测试用例，这个用例应该会失败，从而确认bug
- 开发人员应该执行修复程序并确保测试通过;根据需要编写额外的测试
- 显示赏金猎人的修复;合并修复回公共回购是一种方法
- 确定赏金猎人是否有任何其他有关修复的反馈
- 赏金评委根据他们对错误的可能性和影响的评估来确定奖励的大小。
- 在整个过程中保持奖励参与者的信息，然后努力避免延误向他们发送奖励
- 有关三种奖励的例子，请参阅[以太坊的赏金计划](https://bounty.ethereum.org/)：

> 支付的奖励价值取决于影响的严重程度。对轻微“无害”错误的奖励从0.05 BTC开始。例如，导致共识问题的主要错误将奖励高达5 BTC。如果存在非常严重的漏洞，可能获得更高的奖励（高达25 BTC）。

[本篇原文链接](https://consensys.github.io/smart-contract-best-practices/software_engineering/)